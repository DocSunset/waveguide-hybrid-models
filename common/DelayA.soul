processor DelayA(int maxDelaySamples)
{
    input stream float audioIn;
    input stream float delay;
    input stream int adjustment;
    output stream float audioOut;

    float[maxDelaySamples] buffer;

    void run()
    {
        wrap<buffer.size> inPoint = 0;
        float lastout = 0;
        loop
        {
            float d = float(delay * processor.frequency) - float(adjustment);
            if (d < 0.5f) d = 0.5f;
            if (d > buffer.size) d = buffer.size;
                    
            int   di = int(d);        // integer part
            float df = d - float(di); // fractional part
            if (df < 0.5)
            {
                di = di - 1;
                df = df + 1;
            }
            float coeff = (1.0f - df) / (1.0f + df);


            let outPoint = wrap<buffer.size>(inPoint - di);
            buffer[inPoint++] = audioIn;
            lastout = float( coeff * (buffer[outPoint] - lastout) 
                           + buffer[wrap<buffer.size>(outPoint - 1)]);
            audioOut << lastout;
            advance();
        }
    }
}
