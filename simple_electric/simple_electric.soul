graph simple_electric  [[main]]
{
    input event soul::midi::Message midiIn;
    input pre_distortion_gain.gainIn distortion_gain [[min: 0, max: 10]];
    input feedback_gain.gainIn feedback [[min: 0, max: 1]];
    input event float feedback_delay_time [[min: 0, max: 5]];
    input pre_distortion_output_gain.gainIn dry [[min: 0, max: 1, default: 0.5]];
    input post_distortion_output_gain.gainIn wet [[min: 0, max: 1]];
    output stream float audioOut;

    let
    {
        midiParser = soul::midi::MPEParser;
        mapping = Mapping;
        pluck = NoiseBurst;
        dcblock = DCBlocker;
        string_delay = DelayA(100000);
        filter = OneZero(-1);
        string_loss = VCA;
        distortion = Cubic((-1.0f / 3.0f), 0.0f, 1.0f, 0.0f);
        clipping = Clamp(-2.0f/3.0f, 2.0f/3.0f);
        feedback_delay = DelayL(100000);
        pre_distortion_gain = EvVCA;
        feedback_gain = EvVCA;
        pre_distortion_output_gain = EvVCA;
        post_distortion_output_gain = EvVCA;
        delay_adjustment = Constant(int, 1);
    }

    connection
    {
        midiIn -> midiParser -> mapping; 
        mapping.burst -> pluck.burst;
        mapping.string_len -> string_delay.delay;
        mapping.string_loss -> string_loss.gainIn;
        feedback_delay_time -> EvExpSmooth -> feedback_delay.delay;

        pluck, feedback_delay -> dcblock;
        dcblock -> string_delay.audioIn; 
        string_delay -> filter.audioIn;
        filter -> string_loss.audioIn;
        string_loss -> [1] -> dcblock;

        dcblock -> pre_distortion_gain.audioIn;
        pre_distortion_gain.audioOut -> distortion -> clipping;
        clipping -> feedback_gain.audioIn;
        feedback_gain -> [1] -> feedback_delay.audioIn;
        
        dcblock -> pre_distortion_output_gain.audioIn;
        clipping -> post_distortion_output_gain.audioIn; 
        pre_distortion_output_gain, post_distortion_output_gain -> audioOut;

        delay_adjustment -> string_delay.adjustment;
        delay_adjustment -> feedback_delay.adjustment;
    }
}

//==============================================================================
processor Mapping
{
    input event (soul::note_events::NoteOn,
                 soul::note_events::NoteOff,
                 soul::note_events::Control) eventIn;

    output event float burst;
    output stream float string_len;
    output stream float string_loss;

    float duration;
    int num_active;

    event eventIn (soul::note_events::NoteOn e)
    {
        let freq = soul::noteNumberToFrequency(e.note);
        duration = 1.0f/freq;
        num_active++;
        burst << duration;
    }
    
    event eventIn (soul::note_events::NoteOff e)
    {
        num_active--;
    }

    void run() { 
        loop{ 
            let l = num_active > 0 ? 0.99999f : 0.1f;
            string_len << duration; 
            string_loss << l;
            advance(); 
        } 
    }
}
