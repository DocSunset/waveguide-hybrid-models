graph blotar  [[main]]
{
    input event soul::midi::Message midiIn;
    input pre_distortion_output_gain_coeff.eventIn dry [[min: 0, max: 1, init: 0.5]];
    input post_distortion_output_gain_coeff.eventIn wet [[min: 0, max: 1]];
    input breath.noise breath_noise [[min: 0, max: 1]];
    input breath.pressure breath_pressure [[min: 0, max: 1]];
    input pre_distortion_gain_coeff.eventIn distortion_gain [[min: 0, max: 5, init: 1]];
    input feedback_gain_coeff.eventIn feedback [[min: 0, max: 1]];
    input loss.eventIn stringLoss [[min: 0, max: 1, init: 0.998]];
    input mapping.jet_delay_ratio [[min: 0, max: 2]];
    input crossfade.eventIn filterType [[min: 0, max: 1, init: 1.0f]];
    output stream float audioOut;

    let
    {
        midiParser = soul::midi::MPEParser;
        mapping = Mapping;
        pluck = NoiseBurst;
        breath = NoisyBreath;
        dcblock = DCBlocker;
        string_delay = DelayA(100000);
        filter = BlotarFilter;
        string_loss = soul::gain::DynamicGain(float);
        distortion = Cubic(-1.0f, 0.0f, 1.0f, 0.0f);
        clipping = Clamp(-2.0f/3.0f, 2.0f/3.0f);
        feedback_delay = DelayL(100000);
        pre_distortion_gain_coeff = EvExpSmooth;
        feedback_gain_coeff = EvExpSmooth;
        pre_distortion_output_gain_coeff = EvExpSmooth;
        post_distortion_output_gain_coeff = EvExpSmooth;
        pre_distortion_gain = soul::gain::DynamicGain(float);
        feedback_gain = soul::gain::DynamicGain(float);
        pre_distortion_output_gain = soul::gain::DynamicGain(float);
        post_distortion_output_gain = soul::gain::DynamicGain(float);
        delay_adjustment = Constant(int, 1);
        loss = EvExpSmooth;
        crossfade = EvExpSmooth;
    }

    connection
    {
        midiIn -> midiParser -> mapping.eventIn; 
        mapping.burst -> pluck.burst;
        mapping.string_len -> string_delay.delay;
        mapping.jet_len -> feedback_delay.delay;
        loss -> string_loss.gain;
        crossfade -> filter.crossfade;
        pre_distortion_gain_coeff -> pre_distortion_gain.gain;
        feedback_gain_coeff -> feedback_gain.gain;
        pre_distortion_output_gain_coeff -> pre_distortion_output_gain.gain;
        post_distortion_output_gain_coeff -> post_distortion_output_gain.gain;

        pluck, feedback_delay, string_loss -> string_delay.audioIn;
        string_delay -> dcblock;
        dcblock -> filter.audioIn;
        filter -> [1] -> string_loss.in;

        string_delay, breath -> pre_distortion_gain.in;
        pre_distortion_gain -> distortion -> clipping;
        clipping -> feedback_gain.in;
        feedback_gain -> [1] -> feedback_delay.audioIn;
        
        dcblock -> pre_distortion_output_gain.in;
        clipping -> post_distortion_output_gain.in; 
        pre_distortion_output_gain, post_distortion_output_gain -> audioOut;

        delay_adjustment -> string_delay.adjustment;
        delay_adjustment -> feedback_delay.adjustment;
    }
}

//==============================================================================
processor Mapping
{
    input event (soul::note_events::NoteOn,
                 soul::note_events::NoteOff,
                 soul::note_events::Control) eventIn;
    
    input event float jet_delay_ratio;

    output event float burst;
    output stream float string_len;
    output stream float jet_len;
    output stream float string_loss;

    float s, jet, ratio;
    int num_active;

    event jet_delay_ratio (float r)
    {
        ratio = r;
        jet = ratio * s;
    }

    event eventIn (soul::note_events::NoteOn e)
    {
        let freq = soul::noteNumberToFrequency(e.note);
        s = 1.0f/freq;
        jet = ratio * s;
        num_active++;
        burst << s;
    }
    
    event eventIn (soul::note_events::NoteOff e)
    {
        num_active--;
    }

    void run() { 
        loop{ 
            let l = num_active > 0 ? 0.99999f : 0.1f;
            string_len << s; 
            jet_len << jet; 
            string_loss << l;
            advance(); 
        } 
    }
}

graph BlotarFilter
{
    input stream float audioIn;
    output stream float audioOut;
    input fade.fade crossfade;

    let
    {
        pole = OnePole(0.7f);
        zero = OneZero(-1.0f);
        fade = LinearCrossfade(float);
    }

    connection
    {
        audioIn -> pole, zero;
        pole -> fade.left;
        zero -> fade.right;
        fade -> audioOut;
    }
}
